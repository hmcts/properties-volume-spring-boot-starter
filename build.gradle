plugins {
  id 'checkstyle'
  id 'jacoco'
  id 'pmd'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
  id 'org.springframework.boot' version '2.1.2.RELEASE'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'org.owasp.dependencycheck' version '4.0.2'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'org.sonarqube' version '2.7'
  id 'java-library'
  id 'maven-publish'
  id 'net.ltgt.apt' version '0.21'
}

group 'uk.gov.hmcts.reform'
version '0.0.3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

test {
  useJUnitPlatform()
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  useJUnitPlatform()
}

task printVersion {
  doLast {
    print project.version
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.14'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'checkstyle')
}

pmd {
  toolVersion = "6.9.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("pmd/ruleset.xml")
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'owasp/suppressions.xml'
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn test, integration
sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Propertiesvolume starter"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

def gitRepo = "https://github.com/hmcts/properties-volume-spring-boot-starter"

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourcesJar
      artifact javadocJar

      pom {
        name = 'properties-volume-spring-boot-starter'
        description = 'HMCTS properties volume spring boot starter'
        url = gitRepo
        licenses {
          license {
            name = 'MIT License, Copyright (c) 2019 HM Courts & Tribunals Service'
            url = "http://www.opensource.org/licenses/mit-license.php"
          }
        }
        scm {
          url = gitRepo
        }
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = 'propertiesvolume-spring-boot-starter'
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = gitRepo
    version {
      name = project.version
    }
  }
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

def versions = [
  junit: '5.4.0',
]

dependencies {
  compile(
    "org.springframework.boot:spring-boot-starter-web",
    "org.springframework.boot:spring-boot-starter-validation",
    "org.springframework.cloud:spring-cloud-context:2.1.0.RELEASE",
  )

  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
  
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testRuntimeOnly group: 'org.springframework.boot', name: 'spring-boot-starter-webflux'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath

}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

test {
  useJUnitPlatform()
}

bootJar {
  enabled = false
}

jar {
  enabled = true
}
